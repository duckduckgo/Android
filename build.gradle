// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

    ext {
        kotlin_version = '1.9.20'
        spotless = '6.1.2'
        anvil_version = '2.4.8'
        ksp_version = '1.9.20-1.0.14'
        gradle_plugin = '8.1.2'
        min_sdk = 23
        target_sdk = 33
        compile_sdk = 34
        fladle_version = '0.17.4'
        kotlinter_version = '3.12.0'
        dokka_version = '1.8.20'

        // Calculate lint_version (must always be gradle_plugin + 23)
        def components = gradle_plugin.split('\\.')
        lint_version = gradle_plugin.replaceFirst(components[0], (components[0].toInteger() + 23).toString())
    }

    repositories {
        google()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$gradle_plugin"
        classpath "com.diffplug.spotless:spotless-plugin-gradle:$spotless"
        classpath "com.squareup.anvil:gradle-plugin:$anvil_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id 'org.jetbrains.dokka' version "$dokka_version"
    id 'com.osacky.fulladle' version "$fladle_version"
    id 'org.jmailen.kotlinter' version "$kotlinter_version" apply false
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
    configurations.all {
        resolutionStrategy.force 'org.objenesis:objenesis:2.6'
    }
}

subprojects {

    if (project.name.endsWith("api")) {
        apply plugin: 'org.jetbrains.dokka'
    }

    String[] allowAndroidTestsIn = ["app", "sync-lib", "httpsupgrade-impl"]
    if (!allowAndroidTestsIn.contains(project.name)) {
        project.projectDir.eachFile(groovy.io.FileType.DIRECTORIES) { File parent ->
            if (parent.name == "src") {
                parent.eachFile(groovy.io.FileType.DIRECTORIES) { child ->
                    if (child.name == "androidTest") {
                        throw new GradleException("Only the app module can have Android Tests. Perhaps you could use Robolectric?")
                    }
                }
            }
        }
    }

    String[] allowStringsIn = ["app"]
    if (!allowStringsIn.contains(project.name)) {
        project.projectDir.eachDirRecurse { File parent ->
            if (parent.name == "values") {
                parent.eachFile(groovy.io.FileType.FILES) { child ->
                    if (child.name == "strings.xml") {
                        throw new GradleException("Duplicate strings.xml file found in $project. " +
                                "Only :app should contain the strings.xml file. " +
                                "Rename file to strings-<feature>.xml instead.")
                    }
                }
            }
        }
    }

    def projectPath = path
    configurations.configureEach {
        if (name == "compileClasspath" || name.endsWith("CompileClasspath")) {
            incoming.beforeResolve {

                plugins.each { plugin ->
                    // API modules cannot use the anvil plugin
                    if (projectPath.endsWith("api")) {
                        if (plugin.toString().contains("com.squareup.anvil.plugin")) {
                            throw new GradleException("Invalid plugin $projectPath -> " +
                                    "'api' modules can't use the anvil plugin")
                        }
                    }
                    if (!projectPath.endsWith(":app") && plugin.toString().toLowerCase().contains("kapt")) {
                        throw new GradleException("Invalid usage of kapt $projectPath -> " +
                                "Use ksp instead of kapt")
                    }
                }

                for (dependency in dependencies) {
                    // API modules cannot depend on dagger/anvil
                    if (projectPath.endsWith("api") && projectPath != ":feature-toggles-api") {
                        def notAllowedDeps = ["anvil", "dagger"]
                        if (notAllowedDeps.contains(dependency.name)) {
                            throw new GradleException("Invalid dependency $projectPath -> " +
                                    "'api' modules can't depend on dagger/anvil")
                        }
                    }

                    if (dependency instanceof ProjectDependency) {
                        def dependencyPath = dependency.dependencyProject.path
                        if (dependencyPath == projectPath) continue
                        // internal modules have to use internalImplementation
                        // when a non internal configuration is built (i.e. PlayDebug) no internal dependencies should be found
                        if (dependencyPath.endsWith('internal') && !name.toLowerCase().contains("internal")) {
                            throw new GradleException("Invalid dependency $projectPath -> $dependencyPath. " +
                                    "'internal' modules must use internalImplementation")
                        }
                        // internal modules can depend on impl
                        if (projectPath.endsWith('internal') && dependencyPath.endsWith('impl')) continue
                        if (!projectPath.endsWith(":app") && dependencyPath.endsWith("impl")) {
                            throw new GradleException("Invalid dependency $projectPath -> $dependencyPath. " +
                                    "Only :app module can depend on 'impl' modules")
                        }
                        // API modules cannot depend on :di
                        if (projectPath.endsWith("api") && dependencyPath == ":di") {
                            throw new GradleException("Invalid dependency $projectPath -> $dependencyPath. " +
                                    "'api' modules can't depend on the 'di' module")
                        }

                        // API modules can depend on :feature-toggles-api
                        if (projectPath.endsWith("api")
                                && dependencyPath.endsWith("api")
                                && dependencyPath != ":feature-toggles-api"
                                && dependencyPath != ":navigation-api"
                        ) {
                            if (projectPath.endsWith(":feature-toggles-api") && dependencyPath == ":experiments-api") {
                                // noop
                            } else {
                                throw new GradleException("Invalid dependency $projectPath -> $dependencyPath. " +
                                        "'api' modules can't depend on other 'api' modules")
                            }
                        }
                        if (dependencyPath.endsWith(":app")) {
                            throw new GradleException("Invalid dependency $projectPath -> $dependencyPath. " +
                                    "No other module can depend on ':app'")
                        }
                    }
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

fladle {
    configs {
        serviceAccountCredentials.set(project.layout.projectDirectory.file("flank.json"))
        androidTests {
            async.set(false)
            flankVersion.set("21.+")
            testShards.set(2)
            useOrchestrator.set(false)
            recordVideo.set(false)
            testTargets.set([
                    "notAnnotation com.duckduckgo.espresso.PrivacyTest",
                    "notAnnotation com.duckduckgo.espresso.UserJourney",
            ])
            devices.set([
                    ["model": "Pixel3", "version": "30"],
                    ["model": "Nexus6", "version": "23"]
            ])
            localResultsDir.set("fladleResults")
        }
    }
}

apply plugin: 'android-reporting'