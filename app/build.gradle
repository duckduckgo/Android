apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.squareup.anvil'
apply from: '../versioning.gradle'
apply from: "$rootDir/spotless.gradle"

// vtodo VPN - temporarily included for VPN
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.firebase.firebase-perf'

ext {
    USE_ORCHESTRATOR = project.hasProperty('orchestrator') ? project.property('orchestrator') : false
}

android {
    compileSdkVersion compile_sdk
    ndkVersion '21.0.6113669'
    defaultConfig {
        applicationId "com.duckduckgo.mobile.android.vpn"
        minSdkVersion min_sdk
        targetSdkVersion target_sdk
        versionCode buildVersionCode()
        versionName buildVersionName()
        testInstrumentationRunner "com.duckduckgo.app.TestRunner"
        archivesBaseName = "duckduckgo-$versionName"
        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        sourceSets {
            androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
            androidTest.resources.srcDirs += files("$projectDir/../submodules/".toString())
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    signingConfigs {
        release
    }
    buildTypes {
        debug {
            pseudoLocalesEnabled false
            manifestPlaceholders = [
                    appIcon: "@mipmap/ic_launcher_blue",
                    appIconRound: "@mipmap/ic_launcher_blue_round"
            ]
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            manifestPlaceholders = [
                    appIcon: "@mipmap/ic_launcher_black",
                    appIconRound: "@mipmap/ic_launcher_silhouette_black"
            ]
        }
    }
    externalNativeBuild {

        cmake {
            path "CMakeLists.txt"
        }
    }
    lintOptions {
        abortOnError false
    }
    testOptions {
        unitTests.returnDefaultValues = true

        if (USE_ORCHESTRATOR) {
            execution 'ANDROID_TEST_ORCHESTRATOR'
        }
    }

    packagingOptions {
        exclude "**/attach_hotspot_windows.dll"
        exclude "META-INF/licenses/**"
        exclude "META-INF/AL2.0"
        exclude "META-INF/LGPL2.1"
    }

    def staticConfigPath = "${System.getenv('HOME')}/jenkins_static/com.duckduckgo.mobile.android"
    def propertiesPath = "${staticConfigPath}/ddg_android_build.properties"
    def propertiesFile = new File(propertiesPath)
    if (propertiesFile.exists()) {
        def props = new Properties()
        props.load(new FileInputStream(propertiesFile))
        android.signingConfigs.release.storeFile = file("${staticConfigPath}/${props['key.store']}")
        android.signingConfigs.release.storePassword = props['key.store.password']
        android.signingConfigs.release.keyAlias = props['key.alias']
        android.signingConfigs.release.keyPassword = props['key.alias.password']
    } else {
        println "Signing properties not found at ${propertiesPath}, releases will NOT succeed"
        android.buildTypes.release.signingConfig = null
    }
}

ext {
    workManager = "2.5.0-beta01"
    threetenabp = "1.2.4"
    firebase = "26.0.0"
}


dependencies {
    implementation project(path: ':statistics')
    implementation project(path: ':common')

    implementation project(path: ':di')
    implementation project(path: ':vpn-main')
    implementation project(path: ':vpn-store')

    implementation AndroidX.legacy.supportV4
    debugImplementation Square.leakCanary.android

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation Kotlin.stdlib.jdk7
    implementation KotlinX.coroutines.core
    implementation KotlinX.coroutines.android
    androidTestImplementation (KotlinX.coroutines.test) {
        // https://github.com/Kotlin/kotlinx.coroutines/issues/2023
        // conflicts with mockito due to direct inclusion of byte buddy
        exclude group: "org.jetbrains.kotlinx", module: "kotlinx-coroutines-debug"
    }

    implementation AndroidX.appCompat
    implementation Google.android.material
    implementation AndroidX.constraintLayout
    implementation AndroidX.swipeRefreshLayout
    implementation AndroidX.webkit
    implementation Square.okHttp3.okHttp
    implementation "com.squareup.okhttp3:okhttp-tls:_"
    implementation Square.retrofit2.retrofit
    implementation Square.retrofit2.converter.moshi
    implementation Square.retrofit2.adapter.rxJava2
    implementation Square.retrofit2.converter.scalars
    implementation "com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:_"
    implementation "io.reactivex.rxjava2:rxjava:_"
    implementation "io.reactivex.rxjava2:rxandroid:_"
    implementation JakeWharton.timber
    implementation Google.dagger.android
    implementation Google.dagger.android.support

    // ThreeTenABP
    implementation "com.jakewharton.threetenabp:threetenabp:$threetenabp"

    // RxRelay
    implementation "com.jakewharton.rxrelay2:rxrelay:_"

    // Anko
    implementation "org.jetbrains.anko:anko-commons:_"
    implementation "org.jetbrains.anko:anko-design:_"

    // Android KTX
    implementation AndroidX.core.ktx
    implementation AndroidX.fragmentKtx

    // ViewModel and LiveData
    implementation AndroidX.lifecycle.extensions
    implementation AndroidX.lifecycle.viewModelKtx
    implementation AndroidX.lifecycle.liveDataKtx

    implementation  AndroidX.lifecycle.commonJava8
    testImplementation AndroidX.archCore.testing
    androidTestImplementation AndroidX.archCore.testing

    // Room
    implementation AndroidX.room.runtime
    implementation AndroidX.room.rxJava2
    implementation AndroidX.room.ktx
    kapt AndroidX.room.compiler
    testImplementation AndroidX.room.testing
    androidTestImplementation AndroidX.room.testing

    // WorkManager
    implementation AndroidX.work.runtimeKtx
    implementation "androidx.work:work-multiprocess:$workManager"
    androidTestImplementation AndroidX.work.testing
    implementation AndroidX.work.rxJava2

    // Dagger
    kapt Google.dagger.android.processor
    kapt Google.dagger.compiler
    kaptAndroidTest "com.google.dagger:dagger-android-processor:_"
    kaptAndroidTest "com.google.dagger:dagger-compiler:_"

    // Glide
    implementation "com.github.bumptech.glide:glide:_"
    implementation "com.github.bumptech.glide:okhttp3-integration:_"
    kapt "com.github.bumptech.glide:compiler:_"

    // Lottie
    implementation "com.airbnb.android:lottie:_"

    // Play Store referrer library
    implementation("com.android.installreferrer:installreferrer:_")

    // Testing dependencies
    testImplementation "junit:junit:_"

    androidTestImplementation AndroidX.test.runner
    androidTestImplementation AndroidX.test.rules
    androidTestUtil AndroidX.test.orchestrator
    androidTestImplementation Testing.mockito.android
    androidTestImplementation Testing.mockito.kotlin
    androidTestImplementation Square.okHttp3.mockWebServer
    androidTestImplementation "org.jetbrains.kotlin:kotlin-reflect:_"

    implementation platform("com.google.firebase:firebase-bom:$firebase")
    implementation "com.google.firebase:firebase-perf"
    implementation "com.google.firebase:firebase-crashlytics-ktx"
}
